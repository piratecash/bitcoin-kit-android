plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'org.bouncycastle' && details.requested.name == 'bcprov-jdk15on') {
                details.useTarget 'org.bouncycastle:bcprov-jdk15to18:1.80'
            }
        }
    }
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    namespace 'io.horizontalsystems.bitcoinkit.demo'
    compileSdk 34

    defaultConfig {
        applicationId "io.horizontalsystems.bitcoinkit.demo"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 1
        versionName "0.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "WORDS", "${localProperties['words']}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions { jvmTarget = '17' }
    packagingOptions {
        resources {
            pickFirsts += ['META-INF/atomicfu.kotlin_module']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.appcompat
    implementation libs.constraintlayout
    implementation libs.material

    implementation libs.rxjava
    implementation libs.rxandroid

    // ViewModel and LiveData
    implementation libs.lifecycle.viewmodel.ktx

    //LeakCanary
    debugImplementation libs.leakcanary.android

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation project(':bitcoinkit')
    implementation project(':dashkit')
    implementation project(':bitcoincashkit')
    implementation project(':litecoinkit')
    implementation project(':ecashkit')
    implementation project(':dogecoinkit')
    implementation project(':cosantakit')
    implementation project(':piratecashkit')
}
